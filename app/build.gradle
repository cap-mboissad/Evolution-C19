apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.kouelaa.evolutionc19"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 12
        versionName "1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "version"
    productFlavors {
        prod {
            resValue "string", "app_name", "Evolution C19"
            buildConfigField "String", "ENDPOINT", "\"https://storage.googleapis.com/\""
        }
    }

    signingConfigs {
        release {
            Properties props = new Properties()
            props.load(new FileInputStream(file("../keystore/release.properties")))
            storeFile file(props['storeFile'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    def timber_version = '4.7.1'
    def coroutines_version = '1.3.4'
    def retrofit_version = '2.7.2'
    def okhttp_version = '4.4.0'
    def mpchart_version = 'v3.1.0'
    def threetenbp_version = '1.2.2'
    def firebase_version = '17.2.3'
    def crashlytics_version = '17.0.0-beta03'
    def remote_config_version = '19.1.3'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Kotlin + Coroutine
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // UI
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.google.android.material:material:1.1.0'

    // MPAndroidChart
    implementation "com.github.PhilJay:MPAndroidChart:$mpchart_version"

    // DI
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // Logger
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // LocalDateTime
    implementation ("com.jakewharton.threetenabp:threetenabp:$threetenbp_version"){
        because 'we require api < 26 to use LocalDateTime'
    }

    // Firebase + Chrashlytics + Analytics
    implementation "com.google.firebase:firebase-analytics:$firebase_version"
    implementation "com.google.firebase:firebase-crashlytics:$crashlytics_version"
    implementation "com.google.firebase:firebase-config-ktx:$remote_config_version"


    implementation project(':data')
    implementation project(':domain')
}
